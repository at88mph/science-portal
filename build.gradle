import org.apache.tools.ant.filters.ReplaceTokens


plugins {
    id 'war'
    id 'java'
    // IntelliJ IDEA plugin here to allow integration tests to appear properly in IDEs.
    id 'idea'
}

repositories {
    mavenLocal()
    jcenter()

    maven {
        url = 'http://www.cadc-ccda.hia-iha.nrc-cnrc.gc.ca/m2repo'
    }
}

dependencies {
    implementation 'ca.nrc.cadc:web-test:[2.0,)'
    implementation 'commons-net:commons-net:3.6'
    implementation 'log4j:log4j:1.2.17'
    implementation 'org.opencadc:cadc-web-util:[1.2.7,1.3.0)'
    implementation 'org.seleniumhq.selenium:selenium-java:3.+'
    implementation 'com.opencsv:opencsv:[5.1,6.0)'

    runtime 'javax.servlet.jsp.jstl:jstl-api:1.2'
    runtime 'javax.servlet:jstl:1.2'
    runtime 'jstl:jstl:1.+'
    runtime 'org.apache.taglibs:taglibs-standard-impl:1.2.5'
}

sourceCompatibility = '1.8'
group = 'ca.nrc.cadc'
version = '1005'

ext {
    intTest_user_name = 'CADCtest'
    intTest_user_password = 'sywymUL4'

    // Change this value to test other locations, such as Production or RC.
    intTest_default_web_app_url = 'https://rc.canfar.net'
    intTest_default_web_app_endpoint = '/science-portal'
}

war {
    // Trying to fix static file caching on new build.
    eachFile {
        if (it.name.endsWith('.jsp')) {
            filter(ReplaceTokens, tokens: [version: project.version])
        }
    }

    archiveName = 'science-portal##' + project.version + '.war'
}

sourceSets {
    intTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/intTest/java')
        }

        // set the intTest resource directory
        resources.srcDir file('src/intTest/resources')

        // also grab the resources from the test source set
        // resources.srcDirs += file('src/test/resources')
        resources.srcDirs += new File(System.getenv('A') + '/test-certificates/')
    }
}


tasks.withType(Test) {
    // Assign all Java system properties from
    // the command line to the tests
    systemProperties System.properties
}


configurations {
    intTestCompile
    intTestCompile.extendsFrom(testCompile)
}

idea {
    module {
        //and some extra test source dirs
        testSourceDirs += file('src/intTest/java')
        scopes.TEST.plus += [ configurations.intTestCompile ]
    }
}

['firefox', 'chrome'].each { driver ->
    task "intTest${driver.capitalize()}"(type: Test) { driverTest ->
        testClassesDirs = sourceSets.intTest.output.classesDirs
        classpath = sourceSets.intTest.runtimeClasspath
        reports {
            html.destination = reporting.file("$name/html")
        }
        dependencies {
            intTestCompile 'junit:junit:4.+'
            intTestCompile 'ca.nrc.cadc:web-test:2.+'
            intTestCompile 'org.seleniumhq.selenium:selenium-java:3.+'
        }

        // set the configuration context
        testClassesDirs = sourceSets.intTest.output.classesDirs
        classpath = sourceSets.intTest.runtimeClasspath

        // run the tests always
        outputs.upToDateWhen { false }

        copy {
            from System.getenv('A') + '/etc/'
            into 'build/resources/intTest'
            include 'DelegationTokenKey.priv'
            rename 'DelegationTokenKey.priv', 'RsaSignaturePriv.key'
        }
        copy {
            from System.getenv('A') + '/etc/'
            into 'build/resources/intTest'
            include 'DelegationTokenKey.pub'
            rename 'DelegationTokenKey.pub', 'RsaSignaturePub.key'
        }


        systemProperty "driver", "${driver}"

        if (project.hasProperty('intTest_selenium_server_url')) {
            systemProperty 'selenium.server.url', project.intTest_selenium_server_url
            logger.info('Running browser tests on ' + project.intTest_selenium_server_url)
        }
        else {
            systemProperty 'selenium.server.url', 'http://cadcint2.cadc.dao.nrc.ca:3444'
        }

        if (project.hasProperty('intTest_web_app_url')) {
            systemProperty 'web.app.url', project.intTest_web_app_url
        }
        else {
            systemProperty 'web.app.url', project.intTest_default_web_app_url
        }

        systemProperty 'user.name', project.intTest_user_name
        systemProperty 'user.password', project.intTest_user_password

        maxParallelForks = 1
    }
}
